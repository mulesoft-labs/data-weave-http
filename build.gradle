import static java.lang.String.valueOf

subprojects {

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'scala'
    apply plugin: 'distribution'

    apply plugin: 'dataweave'
    group = "org.mule.weave"
    version = version

    repositories {
        mavenLocal()
        mavenCentral()        
        maven {
            name "mule-releases"
            url "http://repository.mulesoft.org/nexus/content/repositories/releases/"
        }
        maven {
            name "mule-snapshots"
            url "http://repository.mulesoft.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name "jitpack"
            url "https://jitpack.io"
        }
    }

    dependencies {
        compile group: 'org.mule.weave', name: 'wlang', version: weaveVersion
        compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    }       

    publishing {
        publications.all {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/mulesoft-labs/data-weave-io"
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
        }
    }

    dataweave {
        template = "mule_docs,assciidoc"
    }

    task docs(dependsOn: weavedoc, type: Zip) {
        classifier = "weavedocs"
        from 'build/docs/weavedocs/'
    }

    build.dependsOn("weavedoc")
    
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name "mule-releases"
            url "https://repository.mulesoft.org/nexus/content/repositories/releases/"
        }
        maven {
            name "mule-snapshots"
            url "http://repository.mulesoft.org/nexus/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "org.mule.weave:weave-gradle-plugin:" + weavedocVersion
    }
}
